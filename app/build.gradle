plugins {
    alias(libs.plugins.android.application)
}

android {
    def apk = "SuperLyric-Meizu"
    def pkg = "com.hchen.superlyric.meizu"
    def sdk = 36

    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)
    def keyKeyStorePath = properties.getProperty("keyStoreFile") ?: System.getenv("KEYSTORE_PATH")
    def keyKeyStoreFile = null
    def keyKeyStorePassword = null
    def keyKeyAlias = null
    def keyKeyAliasPassword = null
    if (keyKeyStorePath != null) {
        keyKeyStoreFile = file(keyKeyStorePath)
        keyKeyStorePassword = properties.getProperty('keyStorePassword') ?: System.getenv("KEYSTORE_PASS")
        keyKeyAlias = properties.getProperty('keyAlias') ?: System.getenv("KEY_ALIAS")
        keyKeyAliasPassword = properties.getProperty('keyAliasPassword') ?: System.getenv("KEY_PASSWORD")
    }

    signingConfigs {
        release {
            if (keyKeyStorePath != null) {
                storeFile keyKeyStoreFile
                storePassword keyKeyStorePassword
                keyAlias keyKeyAlias
                keyPassword keyKeyAliasPassword
            }
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }

        debug {
            if (keyKeyStorePath != null) {
                storeFile keyKeyStoreFile
                storePassword keyKeyStorePassword
                keyAlias keyKeyAlias
                keyPassword keyKeyAliasPassword
            }
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    namespace pkg
    compileSdk sdk

    defaultConfig {
        applicationId pkg
        minSdk 30
        targetSdk sdk
        // noinspection HighAppVersionCode
        versionCode 2025052700
        versionName "2.5"

        ndk {
            // noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig true
        aidl true
    }

    aaptOptions.additionalParameters '--allow-reserved-package-id', '--package-id', '0x53'

    android {
        applicationVariants.configureEach { variant ->
            variant.outputs.configureEach { output ->
                outputFileName = "${apk}-v${variant.versionName}-${variant.versionCode}-${variant.buildType.name}.apk"
            }
        }
    }
}

dependencies {
    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    compileOnly libs.api
    implementation libs.annotation
    implementation libs.hooktool
    implementation libs.dexkit
    implementation libs.dexkitcache
    implementation libs.mmkv
    implementation libs.gson
    implementation(libs.superlyricapi) {
        changing = true
    }

    implementation(project(':collect'))
    annotationProcessor(project(':collect'))
}